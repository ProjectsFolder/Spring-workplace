plugins {
	id 'org.springframework.boot' version '2.5.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.liquibase.gradle' version '2.0.3'
	id 'war'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '12'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	liquibaseRuntime.extendsFrom runtime
}

repositories {
	mavenCentral()
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/db.changelog-master-log.yaml'
			driver 'com.mysql.cj.jdbc.Driver'
			url 'jdbc:mysql://localhost:3306/spring_demo'
			username 'root'
			password System.env.DB_PASSWORD
			referenceDriver 'liquibase.ext.hibernate.database.connection.HibernateDriver'
			referenceUrl 'hibernate:spring:com.example.demo.entity?dialect=org.hibernate.dialect.MySQLDialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy'
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'mysql:mysql-connector-java'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	//testImplementation 'org.springframework.security:spring-security-test'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
	implementation 'org.thymeleaf:thymeleaf-spring5'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
	implementation 'org.liquibase:liquibase-core:4.4.1'

	liquibaseRuntime 'mysql:mysql-connector-java'
	liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
	liquibaseRuntime 'org.springframework.boot:spring-boot-starter-security'
	liquibaseRuntime 'org.yaml:snakeyaml'
	liquibaseRuntime 'org.liquibase:liquibase-core:4.4.1'
	//liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.1.1'
	liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:4.3.5'
	liquibaseRuntime sourceSets.main.output
}

test {
	useJUnitPlatform()
}
